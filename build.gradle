/* 
 * Copyright 2015, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'java-library'
	id 'groovy'
	id 'codenarc'
	id 'jacoco'
	id 'distribution'
	id 'maven-publish'
}
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/gradle-support.gradle'
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/maven-publish-support.gradle'

description = 'A Thymeleaf Testing / JUnit bridge to treat each Thymeleaf test file as a JUnit test.'
version  = '5.2.0-SNAPSHOT'
group = 'nz.net.ultraq.thymeleaf'
year = '2015'

def junitVersion = '5.9.2'
def thymeleafVersion = '3.1.2.RELEASE'

configurations.all {
	resolutionStrategy.dependencySubstitution {
		substitute module('org.mockito:mockito-core') using module('org.mockito:mockito-core:4.11.0') because 'Still supporting Java 8'
	}
}

dependencies {
	api 'org.apache.groovy:groovy:4.0.14'
	implementation 'javax.servlet:javax.servlet-api:4.0.1'
	implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
	implementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
	implementation 'org.reflections:reflections:0.10.2'
	implementation "org.thymeleaf:thymeleaf:${thymeleafVersion}"
	implementation "org.thymeleaf.testing:thymeleaf-testing:${thymeleafVersion}"
	runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

	codenarc 'org.codenarc:CodeNarc:3.3.0-groovy-4.0'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.9'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

test {
	useJUnitPlatform()
}

groovydoc {
	link("https://junit.org/junit5/docs/${junitVersion}/api/org.junit.jupiter.params/", 'org.junit.jupiter.params.')
	link("https://javadoc.io/doc/org.thymeleaf/thymeleaf-testing/${thymeleafVersion}/", 'org.thymeleaf.testing.')
	link("https://www.thymeleaf.org/apidocs/thymeleaf/${thymeleafVersion}/", 'org.thymeleaf.')
}

jar {
	manifest {
		attributes('Automatic-Module-Name': 'nz.net.ultraq.thymeleaf.testing.junit')
	}
}
